prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix void: <http://rdfs.org/ns/void#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>

insert {

    ?scheme
        rdfs:label ?label ;
        rdfs:comment ?description.

} where {

    ?scheme a skos:ConceptScheme ;
        dct:title ?title .

    optional { ?scheme dct:alternative ?alternative }
    optional { ?scheme dct:description ?description }

    bind(coalesce(?alternative, ?title) as ?label)

}; # complete concept scheme entry

insert {

    ?scheme void:entities ?entities .

} where {

    {
        select ?scheme (count(?concept) as ?entities) {

            ?scheme a skos:ConceptScheme .

            optional { ?concept skos:inScheme ?scheme }

        }
        group by ?scheme
    }

}; # compute concept scheme size

insert {

    ?concept
        rdfs:label ?pref ;
        rdfs:comment ?definition.

} where {

    ?concept a skos:Concept ;
        skos:prefLabel ?pref .

    optional { ?concept skos:definition ?definition }

} # complete concept entry
