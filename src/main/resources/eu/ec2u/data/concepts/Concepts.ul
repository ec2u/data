prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix void: <http://rdfs.org/ns/void#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

delete {

    ?scheme void:entities ?concepts_ .

} insert {

      ?scheme void:entities ?concepts .

  } where {

    optional { ?scheme void:entities ?concepts_ }

    {
        select ?scheme (count(?concept) as ?concepts) {

            ?scheme a skos:ConceptScheme .

            optional { ?concept skos:inScheme ?scheme }

        }
        group by ?scheme
    }

}; # compute concept scheme size

insert {

    ?concept
        rdfs:label ?pref ;
        rdfs:comment ?definition.

} where {

    ?concept a skos:Concept .

    optional { ?concept skos:prefLabel ?pref }
    optional { ?concept skos:definition ?definition }

} # complete concept entry