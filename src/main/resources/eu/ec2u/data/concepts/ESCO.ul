prefix owl: <http://www.w3.org/2002/07/owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

insert {

    ?topConceptOf skos:hasTopConcept ?concept .

    ?concept a skos:Concept ;

        skos:inScheme ?scheme ;

        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:definition ?definition ;

        owl:sameAs ?concept_ .

} where {

    values ?scheme {
        <https://data.ec2u.eu/concepts/esco-skills>
        <https://data.ec2u.eu/concepts/esco-occupations>
    }

    ?concept_ a skos:Concept ;
        skos:inScheme/^owl:sameAs ?scheme .

    bind(iri(concat(str(?scheme), '/', md5(str(?concept_) ))) as ?concept)

    optional { ?concept_ skos:topConceptOf/^owl:sameAs ?topConceptOf }

    optional { ?concept_ skos:prefLabel ?prefLabel }
    optional { ?concept_ skos:altLabel ?altLabel }
    optional { ?concept_ skos:definition ?definition }

}; # adopt concepts

insert {

    ?concept

        skos:broader ?broader ;
        skos:narrower ?narrower .

} where {

    ?concept a skos:Concept .

    optional { ?concept owl:sameAs/skos:broader/^owl:sameAs ?broader }
    optional { ?concept owl:sameAs/skos:narrower/^owl:sameAs ?narrower }

} # create connections