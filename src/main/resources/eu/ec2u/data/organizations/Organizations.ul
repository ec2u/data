prefix org: <http://www.w3.org/ns/org#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

insert {

    ?organization rdfs:label ?label

} where {

    ?organization a org:Organization .

    filter not exists { ?organization rdfs:label ?label_ }

    ?organization skos:prefLabel ?pref .

    optional {
        ?organization skos:altLabel ?alt
        filter (lang(?alt) = lang(?pref))
    }

    bind(coalesce(?alt, ?pref) as ?label)

}; # generate label

insert {

    ?organization rdfs:comment ?comment

} where {

    ?organization a org:Organization .

    filter not exists { ?organization rdfs:comment ?comment_ }

    optional {
        ?organization skos:definition ?definition
    }

    bind(?definition as ?comment)

} # generate comment