prefix ec2u: <https://data.ec2u.eu/terms/>

prefix void: <http://rdfs.org/ns/void#>
prefix dct: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>


insert {

    ?dataset
        rdfs:label ?label ;
        rdfs:comment ?description.

} where {

    ?dataset a ec2u:Dataset ;
        dct:title ?title .

    optional { ?dataset dct:alternative ?alternative }
    optional { ?dataset dct:description ?description }

    bind(coalesce(?alternative, ?title) as ?label)

}; # complete entry

insert {

    ?dataset rdfs:member ?entity.

} where {

    ?dataset a ec2u:Dataset ;
        void:rootResource ?class .

    ?entity a ?class .

}; # compute class-based membership


delete {

    ?dataset void:entities ?entities_

} insert {

      ?dataset void:entities ?entities

  } where {

    optional { ?dataset void:entities ?entities_ }

    {
        select ?dataset (count(distinct ?member) as ?entities) {

            ?dataset a ec2u:Dataset .

            optional { ?dataset rdfs:member ?member }

        }
        group by ?dataset
    }

} # compute size
