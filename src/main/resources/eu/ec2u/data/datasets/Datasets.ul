prefix void: <http://rdfs.org/ns/void#>
prefix dct: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

insert {

    ?dataset rdfs:member ?entity.

} where {

    ?dataset a void:Dataset ;
        void:rootResource ?class .

    ?entity a ?class .

}; # compute class-based membership


delete {

    ?dataset
        dct:extent ?entities_ ;
        void:entities ?entities_ .

} insert {

      ?dataset
          dct:extent ?entities ;
          void:entities ?entities .

  } where {

    optional { ?dataset void:entities ?entities_ }

    {
        select ?dataset (count(distinct ?member) as ?entities) {

            ?dataset a void:Dataset .

            optional { ?dataset rdfs:member ?member }

        }
        group by ?dataset
    }

} # compute size
