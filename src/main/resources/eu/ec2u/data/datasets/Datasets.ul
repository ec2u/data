prefix ec2u: <https://data.ec2u.eu/terms/>
prefix void: <http://rdfs.org/ns/void#>
prefix dct: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>


insert {

    ?dataset
        rdfs:label ?title ;
        rdfs:comment ?description.

} where {

    ?dataset a ec2u:Dataset ;
        dct:title ?title ;
        dct:description ?description .

}; # complete entry

insert {

    ?dataset rdfs:member ?entity.

} where {

    ?dataset a ec2u:Dataset ;
        void:rootResource ?class .

    ?entity a ?class .

}; # compute membership

insert {

    ?dataset void:entities ?entities

} where {

        select ?dataset (count(distinct ?entity) as ?entities) {

            ?dataset a ec2u:Dataset ;
                void:rootResource ?class .

            ?entity a ?class .

        }
        group by ?dataset

}; # compute size

insert {

    ?dataset void:subset ?subset .
    ?owner void:subset ?subset .

    ?subset
        ec2u:owner ?owner ;
        void:entities ?entities .


} where {

    bind (uuid() as ?subset)

    {
        select ?dataset ?owner (count(distinct ?entity) as ?entities)
        where {

            ?dataset a ec2u:Dataset ;
                ec2u:owner ?owner ;
                dct:issues [] ;
                void:rootResource ?class .

            ?entity a ?class .

        }
        group by ?dataset ?owner

    }

} # create owner partitions