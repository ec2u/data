prefix void: <http://rdfs.org/ns/void#>
prefix dct: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix js: <http://www.ontotext.com/js#>


insert {

    ?dataset
        rdfs:label ?label ;
        rdfs:comment ?comment.

} where {

    ?dataset a void:Dataset ;
        dct:title ?title .

    optional {
        ?dataset dct:alternative ?alternative
        filter (lang(?alternative) = lang(?title))
    }

    optional { ?dataset dct:description ?description }

    bind(strlang(js:clip(str(coalesce(?alternative, ?title)), 100), lang(?title)) as ?label)
    bind(strlang(js:clip(str(?description), 1000), lang(?description)) as ?comment)


}; # complete entry

insert {

    ?dataset rdfs:member ?entity.

} where {

    ?dataset a void:Dataset ;
        void:rootResource ?class .

    ?entity a ?class .

}; # compute class-based membership


delete {

    ?dataset void:entities ?entities_

} insert {

      ?dataset void:entities ?entities

  } where {

    optional { ?dataset void:entities ?entities_ }

    {
        select ?dataset (count(distinct ?member) as ?entities) {

            ?dataset a void:Dataset .

            optional { ?dataset rdfs:member ?member }

        }
        group by ?dataset
    }

} # compute size
