prefix ec2u: <https://data.ec2u.eu/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix wdt: <http://www.wikidata.org/prop/direct/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>


#### Lift Background Info From Wikidata ################################################################################

insert {

    ?university
        ec2u:location ?location;
        ec2u:country ?country;
		ec2u:students ?students;
  		ec2u:inception ?inception.

} where {

    ?university a ec2u:University; rdfs:seeAlso ?wikidata.

    optional { ?wikidata wdt:P131 ?location }
    optional { ?wikidata wdt:P17  ?country }
    optional { ?wikidata wdt:P2196 ?students_ }
    optional { ?wikidata wdt:P571 ?inception_ }

    bind (xsd:integer(?students_) as ?students)
    bind (strdt(str(year(?inception_)), xsd:gYear) as ?inception)

}
